plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    alias libs.plugins.modDevGradle
    //alias libs.plugins.shadow
    alias libs.plugins.spotless
    alias libs.plugins.lombok
}

version = "${mod_version}"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

sourceSets {
    // Include resources generated by data generators.
    main.resources {
        srcDir 'src/generated/resources'
    }

    client {
        compileClasspath += main.output
        runtimeClasspath += main.output + main.runtimeClasspath
    }

    test {
        compileClasspath += main.output
        runtimeClasspath += main.output + main.runtimeClasspath
    }

    extra {
        compileClasspath += main.output
        runtimeClasspath += main.output + main.runtimeClasspath
    }

    clientExtra {
        compileClasspath += client.output + extra.compileClasspath
        runtimeClasspath += client.output + client.runtimeClasspath + extra.runtimeClasspath
    }
}

configurations {
    // set these as unresolvable so we don't accidentally force all dependents to use them with us
    clientRuntimeOnly.canBeResolved = false
    extraRuntimeOnly.canBeResolved = false
    clientExtraRuntimeOnly.canBeResolved = false

    runtimeClasspath.extendsFrom localRuntime
    extraRuntimeClasspath.extendsFrom extraLocalRuntime
    clientRuntimeClasspath.extendsFrom clientLocalRuntime
    extraClientRuntimeClasspath.extendsFrom clientExtraLocalRuntime
}

apply from: "$rootDir/gradle/scripts/jars.gradle"
apply from: "$rootDir/gradle/scripts/moddevgradle.gradle"
apply from: "$rootDir/gradle/scripts/repositories.gradle"
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/gradle/scripts/resources.gradle"
apply from: "$rootDir/gradle/scripts/publishing.gradle"
apply from: "$rootDir/gradle/scripts/spotless.gradle"


tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:-removal"
    options.compilerArgs << "-Aquiet=true" // Suppress mixin notes
}

lombok {
    version = "1.18.36"
}
