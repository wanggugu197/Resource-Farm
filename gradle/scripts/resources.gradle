String getConfig(String key) {
    return project.properties.get(key)
}

def mod_id = getConfig("mod_id")
def version = getConfig("version")
def mod_license = getConfig("mod_license")
def mod_name = getConfig("mod_name")
def mod_description = getConfig("mod_description")
def mod_url = getConfig("mod_url")
def mod_issue_tracker = getConfig("mod_issue_tracker")

String suffix = System.getenv("VERSION_SUFFIX")
String version_toml = "$version" + (suffix ? "+$suffix" : "")

tasks.register('printVersion') {
    doLast {
        println mod_version
    }
}

// Create run-folders for non-client configs to avoid pollution of dev envs
tasks.matching { it.name.startsWith('prepare') }.configureEach {
    doFirst {
        mkdir('run')
        mkdir('run/server')
        mkdir('run/gametest')
        mkdir('run/data')
    }
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var neoVersion = libs.versions.neoForge.get()
    neoVersion = neoVersion.substring(0, neoVersion.lastIndexOf('.'))
    var configurationVersion = forge.versions.configuration.get()
    configurationVersion = configurationVersion.substring(0, configurationVersion.lastIndexOf('-'))
    var replaceProperties = [
            version             : version_toml,
            mod_id              : mod_id,
            minecraft_version   : "[1.21,)",
            loader_version      : libs.versions.loader.get(),
            neo_version         : neoVersion,
            configuration_version : configurationVersion,
            kjs_version         : forge.versions.kubejs.get().split("\\.")[0],
            ldlib_version       : forge.versions.ldlib.get(),
            jei_version         : forge.versions.jei.get(),
            emi_version         : forge.versions.emi.get(),
            top_version         : forge.versions.theoneprobe.get(),
            jade_version        : forge.versions.jade.get(),
            modernfix_version   : forge.versions.modernfix.get(),
            mod_license         : mod_license,
            mod_name            : mod_name,
            mod_description     : mod_description,
            mod_url             : mod_url,
            mod_issue_tracker   : mod_issue_tracker
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata
